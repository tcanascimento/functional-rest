plugins {
    id 'java'
}

group 'tcanascimento'
version '1.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

apply from: 'gradle/vavr.gradle'
apply from: 'gradle/junit5.gradle'

ext {
    jupiter_version='5.4.0'
    alter_version='1.4.0'
}

configurations {
    exclusions {
        exclude group: 'org.junit'
    }
}

dependencies {
    implementation(group: 'com.github.pureconfig', name: 'pureconfig-javax_2.12', version: '0.10.2')
}

test {

    description = 'Executa testes. Para executar tags especÃ­ficas, use: gradle clean test -Dtag="tag".'

    String tag = System.properties.getProperty("tag")
    systemProperties = [
            'junit.jupiter.extensions.autodetection.enabled': 'true',
            'junit.jupiter.testinstance.lifecycle.default': 'per_class'
    ]

    useJUnitPlatform() {

        if(null != tag && !tag.equalsIgnoreCase("")) includeTags String.valueOf(tag)
    }

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: ${descriptor}")
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}